apply plugin: 'application'

group 'org.comroid'
version '1.00-SNAPSHOT'

apply from: 'gradle/vars.gradle'

mainClassName = 'org.comroid.wernicke.Bot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.findByName('wrapper')?.configure {
    gradleVersion = '6.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenLocal()
    maven {
        url "https://maven.jetbrains.space/comroid/release"
        credentials.username "Anonymous.User"
        credentials.password "anonymous"
    }
    maven {
        url "https://maven.jetbrains.space/comroid/repo"
        credentials.username "Anonymous.User"
        credentials.password "anonymous"
    }
    jcenter()
    maven { url 'https://www.jitpack.io' }
}

evaluationDependsOn ':guardian:uniform'
evaluationDependsOn ':guardian:restless'

dependencies {
    // annotations
    compileOnly 'org.jetbrains:annotations:19.0.0'

    if (isAutoDeploy) {
        implementation 'com.github.comroid-git:cli-parser:master-SNAPSHOT'

        // status server api
        implementation 'com.github.comroid-git:comroid.org:master-SNAPSHOT'

        // guardian
        implementation 'com.github.comroid-git.guardian-framework:uniform:master-SNAPSHOT'
        implementation 'com.github.comroid-git.guardian-framework:restless:master-SNAPSHOT'

        // javacord Utilities
        implementation 'com.github.comroid-git:javacord-utilities:master-SNAPSHOT'
        implementation 'com.github.comroid-git.botsuite:dux-javacord:master-SNAPSHOT'
    } else {
        implementation project(':clipa')

        // status server api
        implementation project(':status:status-api')

        // guardian
        implementation project(':guardian:restless')
        implementation project(':guardian:uniform').sourceSets.java8compat.output.classesDirs
        implementation project(':guardian:restless').sourceSets.java8compat.output.classesDirs

        // botsuite
        implementation project(':botsuite:javacord')
    }

    // javacord
    implementation 'org.javacord:javacord:3.0.6'

    // skribbl zeug
    implementation 'io.socket:socket.io-client:1.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.0'

    // fuzzy matcher
    implementation 'me.xdrop:fuzzywuzzy:1.3.1'

    implementation 'com.google.flogger:flogger-log4j2-backend:0.5.1'
    runtimeOnly 'org.apache.logging.log4j:log4j-core:2.13.3'
    runtimeOnly 'com.squareup.okhttp3:okhttp:4.7.2'
}
